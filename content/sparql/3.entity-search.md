---
title: "EntitySearch"
description: "Requête utilisant l'api EntitySearch de wikidata"
---

```sparql
SELECT  ?num  ?item  ?itemLabel ?itemDescription ?articleLabel
# SAMPLE et GROUP_CONCAT évitent les doublons dans les résultats (images et natures multiples pour 1 item)
(SAMPLE(?image) as ?image)  # garde seulement la 1ere image 
(GROUP_CONCAT(distinct ?natureLabel;separator=" | ") as ?natureLabels) # natures de chaque item séparés par des "|"
WHERE {
  SERVICE wikibase:mwapi {
    bd:serviceParam wikibase:endpoint "www.wikidata.org";
                    wikibase:api "EntitySearch";
                    mwapi:search "__REPLACE__";
                    mwapi:language "fr".

    ?item wikibase:apiOutputItem mwapi:item.
    ?num wikibase:apiOrdinal true.
  }
  ?item (wdt:P279|wdt:P31) ?nature.
#   ?item wdt:P31 wd:Q5.

  ?article schema:about ?item .
  ?article schema:isPartOf <https://fr.wikipedia.org/>.   

  OPTIONAL { ?item wdt:P18 ?image }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],fr". }
  
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],fr".
    ?nature rdfs:label ?natureLabel.  # label utilisé dans GROUP_CONCAT
  }

} 
GROUP BY ?item ?itemLabel ?itemDescription  ?num ?articleLabel
ORDER BY ASC(?num)
```
